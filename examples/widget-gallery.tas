fun Title(text) {
  Text {
    text:   text
    align:  "center"
    weight: "light"
    size:   2
  }
}

fun GalleryText() {
  Column {
    Title { text: "Text" }

    Text { text: "Simple text" }

    Text { text: "A little bigger", size: 1.1 }

    Text {
      text:   "And bold"
      size:   1.1
      weight: "bold"
    }

    Text {
      text:   "Bigger and light"
      size:   1.5
      weight: "light"
    }

    Text {
      text:   "With a different color"
      size:   1.5
      weight: "light"
      color:  "teal"
    }

    Text {
      text:   "Centered !"
      size:   1.5
      weight: "light"
      color:  "teal"
      align:  "center"
      copy:   true
    }

    Text {
      text:         "Preformated !"
      preformatted: true
    }
  }
}

fun GalleryInputText() {
  var value = ""

  Column {
    Title { text: "InputText" }

    InputText { bindTo: value }

    InputText {
      bindTo:      value
      placeholder: "With placeholder"
    }

    InputText { bindTo: value, multiline: true }

    InputText { bindTo: value, disabled: true }

    InputText {
      bindTo:      value
      placeholder: "With placeholder"
      disabled:    true
    }

    InputText {
      bindTo:    value
      multiline: true
      disabled:  true
    }

  }
}

fun GalleryCheckbox() {
  var value = false

  Column {
    Title { text: "CheckBox Switch" }
    Row {
      CheckBox { bindTo: value, label: "primary" }
      Switch { bindTo: value, label: "primary" }
    }
    Row {
      CheckBox {
        bindTo:   value
        disabled: true
        label:    "primary disabled"
      }
      Switch {
        bindTo:   value
        disabled: true
        label:    "primary disabled"
      }
    }
    Row {
      CheckBox {
        bindTo:    value
        secondary: true
        label:     "secondary"
      }
      Switch {
        bindTo:    value
        secondary: true
        label:     "secondary"
      }
    }
    Row {
      CheckBox {
        bindTo:    value
        disabled:  true
        secondary: true
        label:     "secondary disabled"
      }
      Switch {
        bindTo:    value
        disabled:  true
        secondary: true
        label:     "secondary disabled"
      }
    }
  }
}

fun GalleryButton() {
  var value = false
  var disabled = false

  Column {
    Title { text: "Button" }

    Switch { bindTo: disabled, label: "Disabled" }

    Button { text: "Click me", disabled: disabled }

    Button {
      text:     "Confirm"
      confirm:  "Are you sure ?"
      onClick:  () => log("Confirmed !")
      disabled: disabled
    }

    Button {
      text:      "Secondary"
      secondary: true
      disabled:  disabled
    }

    Button {
      text:     "Outline"
      outline:  true
      disabled: disabled
    }

    Button {
      text:      "Outline Secondary"
      secondary: true
      outline:   true
      disabled:  disabled
    }

    Button {
      text:     "Link"
      link:     true
      disabled: disabled
      onClick:  () => log("link click")
    }

    Button {
      text:      "Link Secondary"
      secondary: true
      link:      true
      disabled:  disabled
      onClick:   () => log("link click secondary")
    }
  }
}

fun GalleryLink() {
  var value = false

  Column {
    Title { text: "Link" }
    Link { url: "https://www.example.com" }
  }
}

fun GalleryPager() {
  var currentPage = 1
  var showPrevNext = true
  var disabled = false
  var size = "2"

  Column {
    Title { text: "Pager" }

    Switch {
      bindTo: showPrevNext
      label:  "showPrevNext"
    }

    Switch { bindTo: disabled, label: "disabled" }

    Row {
      Select {
        bindTo:  size
        options: ["2", "3", "4"]
      }
      Text { text: "size" }
    }

    Pager {
      max:          100
      perPage:      10
      bindTo:       currentPage
      showPrevNext: showPrevNext
      size:         string_to_number(size)
      disabled:     disabled
    }
  }
}

fun GallerySelect() {
  var values = ["Red", "Green", {value: "#00F", label: "Blue"}]
  var value = "Red"
  var value2
  var disabled = false

  Column {
    Title { text: "Select Radio" }

    Switch { bindTo: disabled, label: "Disabled" }

    Select {
      options:     values
      placeholder: "Color"
      bindTo:      value
      disabled:    disabled
    }
    Select {
      options:     values
      placeholder: "Color"
      bindTo:      value
      disabled:    disabled
    }
    Text { text: "Selected: " + value }
    Select {
      options:     values
      placeholder: "Color"
      bindTo:      value
    }
    Select {
      options:     values
      placeholder: "Color"
      bindTo:      value2
      disabled:    disabled
    }

    Row {
      values
         | array_map(option => Radio {
            option:   option
            bindTo:   value
            disabled: disabled
            label:    if (typeof(option) == "record") {
              option.label
            } else {
              option
            }
          })
    }

    Row {
      values
         | array_map(option => Radio {
            option:    option
            bindTo:    value
            disabled:  disabled
            secondary: true
            label:     if (typeof(option) == "record") {
              option.label
            } else {
              option
            }
          })
    }
  }
}

fun GalleryTable() {
  var useRemaining = false
  var noHeader = false
  var striped = true
  var noHighlight = false
  var bordered = false

  let data = [
    {name: "Toto", age: "32"}
    {name: "Tata", age: "40"}
    {name: "Titi", age: "13"}
    {name: "Tutu", age: "132", secret: true}
  ]

  Column {
    Title { text: "Table" }

    CheckBox {
      bindTo: useRemaining
      label:  "Name column must use remaining space"
    }

    CheckBox { bindTo: noHeader, label: "No header" }

    CheckBox { bindTo: striped, label: "Striped" }

    CheckBox {
      bindTo: noHighlight
      label:  "No Highlight"
    }

    CheckBox { bindTo: bordered, label: "Bordered" }

    Table {
      _key:        row => row.name
      noHeader:    noHeader
      striped:     striped
      noHighlight: noHighlight
      bordered:    bordered
      columns:     [
        {
          description:  "Name"
          display:      row => Text { text: row.name }
          useRemaining: useRemaining
        }
        {description: "Age", display: row => Text { text: row.age }}
      ]
      values:      data
    }

    Table {
      _key:        row => row.name
      values:      data
      noHeader:    noHeader
      noHighlight: noHighlight
      bordered:    bordered
      striped:     striped
    }

    Table {
      _key:        row => row.name
      noHeader:    noHeader
      noHighlight: noHighlight
      striped:     striped
      bordered:    bordered
      columns:     [
        "name"
        {
          description: "Age"
          display:     row => Text { text: row.age + " years old" }
        }
      ]
      values:      data
    }
  }
}

fun GalleryTabs() {
  var currentTab = "1"
  var hasAfter = false
  let options = [
    {label: "First tab", value: "1"}
    {label: "Second tab", value: "2"}
    {label: "Third tab", value: "3"}
    {label: "Fourth tab", value: "4"}
    {label: "Fifth tab", value: "5"}
    {label: "Sixth tab", value: "6"}
    {label: "Seventh tab", value: "7"}
    {label: "Eighth tab", value: "8"}
  ]

  Column {
    Title { text: "Tabs" }

    Switch { bindTo: hasAfter, label: "Has after" }

    Row {
      Tabs {
        bindTo:  currentTab
        options: options | array_take(2)
        after:   if (hasAfter) {
          Row {
            View {} | flex(1)
            Button { text: "After", outline: true }
          }
        }
      }
    }

    Row {
      Tabs {
        bindTo:  currentTab
        options: options
        after:   if (hasAfter) {
          Row {
            View {} | flex(1)
            Button { text: "After", outline: true }
          }
        }
      }

    }

    switch (currentTab) {
      "1" => Text { text: "First tab" }
      "2" => Text { text: "Second tab" }
      _   => Text { text: "Unknown tab" }
    }
  }
}

fun GalleryLoader() {
  Column {
    Title { text: "Loader" }

    Loader {}
    Loader { primary: true }
    Loader { secondary: true }
    Loader { size: "sm" }
    Loader { size: "md" }
    Loader { size: "lg" }
  }
}

fun GalleryOverlay() {
  var showOverlay
  var position
  var modal

  Column {
    Title { text: "Overlay" }

    Button {
      text:    "Show Dialog"
      onClick: () => {
        set position = "center"
        set modal = false
        set showOverlay = true
      }
    }

    Button {
      text:    "Show Modal"
      onClick: () => {
        set position = "center"
        set modal = true
        set showOverlay = true
      }
    }

    Button {
      text:    "Show Drawer Left"
      onClick: () => {
        set position = "left"
        set modal = true
        set showOverlay = true
      }
    }

    Button {
      text:    "Show Drawer Bottom"
      onClick: () => {
        set position = "bottom"
        set modal = true
        set showOverlay = true
      }
    }

    if (showOverlay) {
      Overlay {
        position: position
        modal:    modal
        title:    "Title here"
        onClose:  () => set showOverlay = false

        Text { text: "Overlay content" }
        Text {
          text: "Press on the backdrop to close it"
        }
      }
    }
  }
}

fun GallerySnippet() {
  var noMaxHeight = false
  var language = "javascript"
  var format = false

  Column {
    Title { text: "Snippet" }

    Switch {
      bindTo: noMaxHeight
      label:  "No max height"
    }

    Switch { bindTo: format, label: "Format" }

    Snippet {
      format:      format
      language:    language
      noMaxHeight: noMaxHeight
      text:        "function filter(array, predicate) {\n    let index = -1;\n    let resIndex = 0;\n    const length = array == null ? 0 : array.length;\n    const result = [];\n\n    while (++index < length) {\n        const value = array[index];\n        if (predicate(value, index, array)) {\n            result[resIndex++] = value;\n        }\n    }\n    return result;\n}"
    }
  }
}

fun App() {
  Column {
    padding: 0.5

    Debug { value: "Debug enabled !" }

    var currentTheme = "toy-box"
    let themes = [
      "html"
      "toy-box"
      "twbs"
      "twbs-dark"
      "nes-css"
      "dark-orange"
      "98"
      "mozaic-lm"
    ]

    Column {
      Row {
        Text { text: "Change theme: " }
        Select {
          value:    currentTheme
          onChange: newVal => {
            set currentTheme = newVal
            set_system_property("theme", newVal)
          }
          options:  themes
        }
      }

      Row {
        wrap: true

        View { width: "360px", GalleryInputText {} }
        View { width: "360px", GalleryText {} }
        View { width: "360px", GalleryCheckbox {} }
        View { width: "360px", GalleryButton {} }
        View { width: "360px", GalleryLink {} }
        View { width: "360px", GalleryPager {} }
        View { width: "360px", GallerySelect {} }
        View { width: "360px", GalleryTable {} }
        View { width: "360px", GalleryTabs {} }
        View { width: "360px", GalleryLoader {} }
        View { width: "360px", GalleryOverlay {} }
        View { width: "360px", GallerySnippet {} }
      }
    }
  }
}

App {}
